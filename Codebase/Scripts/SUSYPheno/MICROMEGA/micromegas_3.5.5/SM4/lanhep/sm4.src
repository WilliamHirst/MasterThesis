%
% Standard Model - unitary and t'Hooft-Feynman gauges.
%

keys gauge_fixing=Feynman.

model 'SM4'/1.


option ReduceGamma5=0.
let g5=gamma5.
use sm_tex.

parameter  EE  = 0.31223 : 'Electromagnetic coupling constant (<->1/128)',
	   GG  = 1.117   : 'Strong coupling constant (Z point)  (PDG-94)',
	   SW  = 0.481  : 'sin of the Weinberg angle (MS-bar)'.

parameter  CW  = sqrt(1-SW**2) : 'cos of the Weinberg angle'.
parameter  MZ=91.187.
parameter  MW=MZ*CW.
parameter  Q=100.
external_func(MbEff,1).
parameter Mb=MbEff(Q).

%let  V=(2*MW/EE*SW).
%let V*EE=2*MW*SW.


let Vud=1,Vus=0,Vub=0,Vcd=0,Vcs=1,Vcb=0,Vtd=0,Vts=0,Vtb=1.

prtcformat fullname:
'Full   Name     ', name:' P ', aname:' aP', pdg:'  number  ',
spin2,mass,width, color, aux, texname: '> LaTeX(A)       <',
atexname:'>  LateX(A+)           <' .



vector  
	A/A: (photon, pdg 22, gauge),
	Z/Z:('Z boson',pdg 23, mass MZ , width wZ = auto, gauge),
	G/G: (gluon, pdg 21,color c8, gauge),
	'W+'/'W-': ('W boson',pdg 24, mass MW, width wW = auto, gauge).

spinor 		ne/Ne:(neutrino,pdg 12,left), 	  e:(electron,pdg 11),
		nm/Nm:('mu-neutrino',pdg 14,left),   m:(muon, pdg 13,mass Mm  = 0.1057),
		nl/Nl:('tau-neutrino',pdg 16,left),  l:('tau-lepton',pdg 15, mass Ml  = 1.777).
 
spinor  '~n'/'~N':('heavy-neutrino',pdg 18, mass MN=60 ),  '~l'/'~L':('heavy-lepton',pdg 17, mass ML  = 80,width wL = auto ).


spinor		u:('u-quark',pdg 2,color c3, mass Mu=0.01),
		d:('d-quark',pdg 1,color c3, mass Md=0.01),
		c:('c-quark',pdg 4,color c3, mass Mc  = 1.300),
		s:('s-quark',pdg 3,color c3, mass Ms = 0.200),
		t:('t-quark',pdg 6,color c3, mass Mtop = 170, width wtop = 1.442),
		b:('b-quark',pdg 5,color c3, mass Mb ).



scalar h/h:('Higgs', pdg 25, mass Mh = 200, width wh = auto).
			
let l1={ne,e},  L1={Ne,E}.
let l2={nm,m},  L2={Nm,M}.
let l3={nl,l},  L3={Nl,L}.
let l4={'~n','~l'}, L4={'~N','~L'}.

let q1={u,d}, Q1={U,D}, q1a={u,Vud*d+Vus*s+Vub*b}, Q1a={U,Vud*D+Vus*S+Vub*B}.
let q2={c,s}, Q2={C,S}, q2a={c,Vcd*d+Vcs*s+Vcb*b}, Q2a={C,Vcd*D+Vcs*S+Vcb*B}. 
let q3={t,b}, Q3={T,B}, q3a={t,Vtd*d+Vts*s+Vtb*b}, Q3a={T,Vtd*D+Vts*S+Vtb*B}.


let B1= -SW*Z+CW*A, W3=CW*Z+SW*A, W1=('W+'+'W-')/Sqrt2,
	 W2 = i*('W+'-'W-')/Sqrt2.


let gh1 = ('W+.c'+'W-.c')/Sqrt2, gh2= i*('W+.c'-'W-.c')/Sqrt2,
		gh3= CW*'Z.c'+SW*'A.c', gh={gh1,gh2,gh3}.

let Gh1 = ('W+.C'+'W-.C')/Sqrt2, Gh2=i*('W+.C'-'W-.C')/Sqrt2, 
		Gh3= CW*'Z.C'+SW*'A.C', Gh={Gh1,Gh2,Gh3}. 


let WW1 = {W1,  W2 , W3}, WW = {'W+',W3,'W-'}.

let g=EE/SW, g1=EE/CW.




% Self-interaction of gauge bosons



lterm -F**2/4   where 
	F=deriv^mu*B1^nu-deriv^nu*B1^mu.

lterm -F**2/4  where
	F=deriv^mu*G^nu^a-deriv^nu*G^mu^a+i*GG*f_SU3^a^b^c*G^mu^b*G^nu^c.

lterm -F**2/4  where
F=deriv^mu*WW1^nu^a-deriv^nu*WW1^mu^a -g*eps^a^b^c*WW1^mu^b*WW1^nu^c.




% left fermion interaction with gauge fields


lterm  	anti(psi)*gamma*(1-g5)/2*(i*deriv-g*taupm*WW/2-Y*g1*B1)*psi
		where 
			psi=l1,  Y=-1/2;
			psi=l2,  Y=-1/2;
			psi=l3,  Y=-1/2;
                        psi=l4,  Y=-1/2;
			psi=q1a, Y= 1/6;
			psi=q2a, Y= 1/6;
			psi=q3a, Y= 1/6.

% right fermion interaction with gauge fields

lterm  	anti(psi)*gamma*(1+g5)/2*(i*deriv - Y*g1*B1)*psi
		where 
			psi=e,Y= -1;
			psi=m,Y= -1;
			psi=l,Y= -1;
                        psi='~l', Y=-1;
                        psi='~n', Y=0;
			psi=u, Y=  2/3;
			psi=c, Y=  2/3;
			psi=t, Y=  2/3;
			psi=d, Y= -1/3;
			psi=s, Y= -1/3;
			psi=b, Y= -1/3.


% quark-gluon interaction

lterm  GG*anti(psi)*lambda*gamma*G*psi where
	psi=q1; psi=q2; psi=q3.



let pp = { -i*'W+.f',  (vev((2*MW/EE*SW))+h+i*'Z.f')/Sqrt2 }, 
    PP = {  i*'W-.f',  (vev((2*MW/EE*SW))+h-i*'Z.f')/Sqrt2 }.


lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*pr*pp + anti(pr)*(1-g5)/2*pl*PP )
    where
	M=Vud*Md,  pl=q1a, pr=d;          % 0 stands for Md 
	M=Vus*Ms, pl=q1a, pr=s;
	M=Vub*Mb, pl=q1a, pr=b;
	M=Vcd*0,  pl=q2a, pr=d;
	M=Vcs*Ms, pl=q2a, pr=s;
	M=Vcb*Mb, pl=q2a, pr=b;
	M=Vtd*0,  pl=q3a, pr=d;
	M=Vts*Ms, pl=q3a, pr=s;
	M=Vtb*Mb, pl=q3a, pr=b.


lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*i*tau2*pr*PP 
		+ anti(pr)*(1-g5)/2*i*pl*tau2*pp ) 
 where
	M=Mu,  pl=q1a, pr=u;
	M=Mc,  pl=q2a, pr=c;
	M=Mtop,pl=q3a, pr=t;
        M=MN  ,pl=l4,  pr='~n'.

lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*pr*pp + anti(pr)*(1-g5)/2*pl*PP )
    where
	M=Mm,  pl=l2,  pr=m;
	M=Ml,  pl=l3,  pr=l;
        M=ML,  pl=l4,  pr='~l'.
	


lterm -2*lambda*(pp*PP-(2*MW/EE*SW)**2/2)**2  where 
	lambda=(g*Mh/MW)**2/16.



let Dpp^mu^a = (deriv^mu+i*g1/2*B1^mu)*pp^a +
	 i*g/2*taupm^a^b^c*WW^mu^c*pp^b.

let DPP^mu^a = (deriv^mu-i*g1/2*B1^mu)*PP^a 
	-i*g/2*taupm^a^b^c*{'W-'^mu,W3^mu,'W+'^mu}^c*PP^b.

	

lterm DPP*Dpp.





lterm -i*GG*f_SU3*ccghost(G)*G^mu*deriv^mu*ghost(G).
lterm  -1/2*(deriv*G)**2.


%lterm -g*eps*gh*WW1*deriv*Gh.

lterm g*eps*deriv*Gh*gh*WW1.


lterm  -1/2*(deriv*A)**2.


lterm  -1/2*(2*(deriv*'W+'+MW*'W+.f')*(deriv*'W-'+MW*'W-.f') +
	(deriv*Z+MW/CW*'Z.f')**2).


lterm -MW*EE/2/SW*((h+i*'Z.f')*('W-.C'*'W+.c' + 'W+.C'*'W-.c')
    		+h*'Z.C'*'Z.c'/CW**2-2*i*'Z.f'*'W+.C'*'W-.c').

lterm i*EE*MW/2/CW/SW*(
	'W+.f'*('W-.C'*'Z.c'*(1-2*SW**2)+'W-.c'*'Z.C'
			+2*CW*SW*'W-.C'*'A.c') -
	'W-.f'*('W+.C'*'Z.c'*(1-2*SW**2)+'W+.c'*'Z.C'
			+2*CW*SW*'W+.C'*'A.c')).


SetAngle(1-SW**2=CW**2).
SetAngle(MW/CW=MZ).
CheckMasses.
CheckHerm.
